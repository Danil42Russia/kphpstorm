plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.6.3'     // https://github.com/JetBrains/gradle-intellij-plugin
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'  // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.jvm
}

group 'com.vk'
// important! update changeNotes (below) on version bump
version '1.1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://www.jetbrains.com/intellij-repository/snapshots'
    }
}
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

idea {
    module {
        excludeDirs = [
            file(".idea"),
            file(".gradle"),
            file("gradle"),
            file("build"),
        ]
    }
}

intellij {
    type 'IU'
    // for release versions: https://www.jetbrains.com/intellij-repository/releases (com.jetbrains.intellij.idea)
    // for EAPs: https://www.jetbrains.com/intellij-repository/snapshots
    version '2020.3'
    plugins = [
        'com.jetbrains.php:203.5981.175', // https://plugins.jetbrains.com/plugin/6610-php/versions
    ]
    tasks {
        // this task fails :( that's why disabled
        "buildSearchableOptions" {
            enabled = false
        }
    }
}
runIde {
    ideDirectory '/Applications/PhpStorm.app/Contents'
//    ideDirectory '/Applications/PhpStorm 2020.3 EAP.app/Contents'
}

patchPluginXml {
    // see https://github.com/JetBrains/gradle-intellij-plugin#patching-dsl
    // <change-notes> is only for LATEST version and should be updated on every version bump
    changeNotes """
<ul>
  <li>better inferring for untyped arrays</li>
  <li>@kphp- tags added in the past 3 months</li>
  <li>able to import an undefined class</li>
  <li>'mixed' instead of 'var'</li>
</ul>
"""
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/fixtures'
        }
    }
}
test {
    exclude 'com/vk/kphpstorm/testing/infrastructure/**'
}
