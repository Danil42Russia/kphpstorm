plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.4.16'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
}

group 'com.vk'
// important! update changeNotes (below) on version bump
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://www.jetbrains.com/intellij-repository/snapshots'
    }
}
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

idea {
    module {
        excludeDirs = [
            file(".idea"),
            file(".gradle"),
            file("gradle"),
            file("build"),
        ]
    }
}

intellij {
    type 'IU'
    // for release versions: https://www.jetbrains.com/intellij-repository/releases (com.jetbrains.intellij.idea)
    // for EAPs: https://www.jetbrains.com/intellij-repository/snapshots
    version '2020.2'
    plugins = [
        'com.jetbrains.php:202.6397.94', // https://plugins.jetbrains.com/plugin/6610-php/versions
    ]
    tasks {
        // this task fails :( that's why disabled
        "buildSearchableOptions" {
            enabled = false
        }
    }
}
runIde {
    ideDirectory '/Applications/PhpStorm.app/Contents'
//    ideDirectory '/Applications/PhpStorm 2020.2 EAP.app/Contents'
}

patchPluginXml {
    // see https://github.com/JetBrains/gradle-intellij-plugin#patching-dsl
    // <change-notes> is only for LATEST version and should be updated on every version bump
    changeNotes """
<ul>
  <li>custom phpdoc type parsers: support tuple, shape, var, future, arbitrary nesting, nullable types</li>
  <li>patched type inferring supporting tuples and shapes + hack php stdlib inferring</li>
  <li>complete and validate @kphp- doc tags</li>
  <li>strict typing inspections</li>
  <li>phpdoc simplification</li>
  <li>custom quick documentation and type info</li>
  <li>support both PHP and KPHP projects</li>
</ul>
"""
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/fixtures'
        }
    }
}
test {
    exclude 'com/vk/kphpstorm/testing/infrastructure/**'
}
